plugins {
    id 'org.unbroken-dome.test-sets' version '1.2.0'
    id 'io.ratpack.ratpack-java' version '1.1.1'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
}

if (!JavaVersion.current().java8Compatible) {
  throw new IllegalStateException("Must be built with Java 8 or higher")
}

apply plugin: "idea"

repositories {
    jcenter()
}

testSets {
    itest
}

dependencies {
    itestCompile ratpack.dependency('test')

    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.10.19'

    // This is a Guice ratpack app
    compile ratpack.dependency("guice")
    compile ratpack.dependency("rx")
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-validator:commons-validator:1.5.0'
    compile 'com.github.davidmoten:rxjava-jdbc:0.7'

    runtime 'org.slf4j:slf4j-simple:1.7.10'
    runtime 'org.postgresql:postgresql:9.4.1207'
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

check.dependsOn itest
itest.mustRunAfter test

// The “ratpack” plugin is IDEA aware.
// It will create a run configuration in IDEA to launch your app in your IDE, with hot reloading.
idea {
    project {
        //use JDK 1.8 in idea
        jdkName "1.8"
        languageLevel "1.8"
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                //configure git support for the project in idea
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}

mainClassName = "com.gbook.profiles.ProfilesApp"
